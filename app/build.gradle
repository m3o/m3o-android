plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
    id 'com.klaxit.hiddensecrets' version '0.2.0' apply true
}

android {
    signingConfigs {
        signingConf {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())

            storeFile file(properties.getProperty("signing.file"))
            storePassword properties.getProperty("signing.password")
            keyAlias properties.getProperty("signing.key.alias")
            keyPassword properties.getProperty("signing.key.password")
        }
    }

    compileSdk 32

    defaultConfig {
        applicationId "com.m3o.mobile"
        minSdk 16
        targetSdk 32
        versionCode 1
        versionName "0.1.0"
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.signingConf
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

dependencies {
    def ktor_version = "1.6.8"
    def navigation_version = "2.5.0"

    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'nl.dionsegijn:konfetti-xml:2.0.2'
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'
    implementation files('libs/m3okotlin.jar')
}